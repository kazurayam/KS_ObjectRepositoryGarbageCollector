plugins {
    id 'groovy'
    id 'maven-publish'
    id 'signing'
}

group = "com.kazurayam"
version = "0.4.13-SNAPSHOT"

import org.gradle.internal.os.OperatingSystem
static String resolveKatalonStudioInstallationDirectory() {
    OperatingSystem os = OperatingSystem.current()
    if (os.isMacOsX()) {
        return '/Applications/Katalon Studio.app/Contents/Eclipse'
    } else if (os.isWindows()) {
        // Users can install Katalon Studio anywhere they like. Therefore it impossible
        // to resolve the installation directory automatically.
        // You need to hard code the path
        String userHome = System.getProperty("user.home")
        return "${userHome}/Katalon_Studio_Windows_64-10.2.0"
    } else if (os.isLinux()) {
        // Consider Linux.
        throw new UnsupportedOperationException("I don't know")
    } else {
        // Unknown OS.
        return new UnsupportedOperationException("Who knows?")
    }
}

ext {
    KATALON_STUDIO_INSTALLATION_DIRECTORY = resolveKatalonStudioInstallationDirectory()
    GroovyVersion = '3.0.17'
    ARTIFACT_ID = 'ks-object-repository-garbage-collector'
    isReleaseVersion = ! version.endsWith("SNAPSHOT")
}

repositories {
    mavenCentral()
    /*
    maven {
        url = uri("https://maven.pkg.github.com/kazurayam/monk-directory-scanner")
        credentials {
            username = project.findProperty('gpr.user')
            password = project.findProperty('gpr.key')
        }
    }
     */
    mavenLocal()
}

dependencies {
    implementation libs.directoryscanner

    // will look into the Katalon Studio installation directory
    implementation fileTree(dir: "${KATALON_STUDIO_INSTALLATION_DIRECTORY}/configuration/resources/lib",
            include: [
                    "groovy-${GroovyVersion}.jar"
            ]
    )
    implementation fileTree(dir: "${KATALON_STUDIO_INSTALLATION_DIRECTORY}/plugins",
            include: [
                    'com.fasterxml.jackson*.jar',
                    'com.google.gson*.jar',

                    // to import com.kms.katalon.core.testobject.TestObject
                    // to import com.kms.katalon.core.testobject.SelectorMethod
                    // to import com.kms.katalon.core.testobject.ObjectRepository
                    'com.kms.katalon.core*.jar',
                    'com.kms.katalon.util*.jar',

                    // to import groovy.lang.GroovyObject
                    '**/org.codehaus.groovy*/lib/groovy*-indy.jar',

                    'org.apache.commons.lang*.jar',
                    'org.dom4j*.jar',
                    'org.eclipse.osgi*.jar',
                    'org.seleniumhq.selenium.selenium-api*.jar',
                    'slf4j*.jar'
            ])

    // will look into the Maven Central repository
    testImplementation libs.junit4
    testImplementation libs.slf4j.simple
}

tasks.register('printVersions') {
    doLast {
        OperatingSystem os = OperatingSystem.current()
        println "${os.toString()}"
        println "Java ${JavaVersion.current()}"
        println "Groovy ${GroovySystem.getVersion()}"
        println GradleVersion.current().toString()
    }
}

import org.gradle.internal.os.OperatingSystem
tasks.register('printClasspath') {
    group = 'Custom'
    description = "show the entries included in the runtime classpath, sorted alphabetically"
    doLast {
        sourceSets.main.runtimeClasspath.sort().each { println it }
    }
}

groovydoc {
    docTitle = ARTIFACT_ID  + ' ' + project.version + " API"
}

tasks.register("publishGroovydoc", Copy) {
    group = 'Custom'
    description = "copy the Groovydoc files into the rootProject/docs directory to make it publicly accessible"
    from 'build/docs/groovydoc'
    into "${rootProject.projectDir}/docs/api"
}
groovydoc.finalizedBy publishGroovydoc

// Apply a specific Java toolchain to ease working with different environments.
java {
    toolchain {
        // Katalon Studio v10.1.0 runs on top of JDK17
        languageVersion = JavaLanguageVersion.of(17)
    }
}

jar {
    archiveBaseName = "${ARTIFACT_ID}"
    include('com/kazurayam/ks/**')
    exclude('internal/**')
}

publishing {
    publications {
        mylib(MavenPublication) {
            groupId = project.group
            artifactId = "${ARTIFACT_ID}"
            from components.java
            pom {
                description = "Tool to find unused TestObject in a project of Katalon Studio"
                url = "https://kazurayam.github.io/KS_ObjectRepositoryGarbageCollector"
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://www.apache.org.licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'kazurayam'
                        name = 'URAYAMA,kazuaki'
                        email = 'kazuaki.urayama@gmail.com'
                    }
                }
                scm {
                    connection = "scm:git:https://github.com/kazurayam/${project.rootProject.name}.git"
                    developerConnection = "scm:git:git@github.com:kazurayam/${project.rootProject.name}.git"
                    url = "https://github.com/kazurayam/${project.rootProject.name}"
                }
            }
        }
    }
    repositories {
        maven {
            name = "gpr"
            url = uri("https://maven.pkg.github.com/kazurayam/${rootProject.name}")
            credentials {
                username = project.findProperty('gpr.user')
                password = project.findProperty('gpr.key')
            }
        }
    }
}
// type "$ gradle publishMylibPublicationToGprRepository" to publish

signing {
    sign publishing.publications.mylib
}

// I do not want to sign the SNAPSHOT
tasks.withType(Sign) {
    onlyIf { isReleaseVersion }
}