plugins {
  id 'groovy'
}

def userHome = System.getProperty('user.home')
def rootProjectDir = rootProject.projectDir

tasks.register('importDrivers') {
    group = 'Custom'
    description = "import the KS_ObjectRepositoryGarbageCollector jar and the MockDirectoryScanner jar into the Drivers folder"
    doFirst {
        delete 'Drivers'
        mkdir 'Drivers'
    }
    doLast {
        copy {
            into layout.projectDirectory.dir("Drivers")
            from("${rootProject.projectDir}/lib/build/libs") {
                include "${rootProject.name}*.jar"
            }
        }
    }
    doLast {
        copy {
            into layout.projectDirectory.dir("Drivers")
            from("$userHome/.m2/repository/com/kazurayam/MonkDirectoryScanner/0.1.0") {
                include '**/*.jar'
            }
        }
    }
}

tasks.register("cleanProject", Delete) {
    group = 'Custom'
    description = "remove the bin folder and others to clean up the Katalon project"
    delete '.cache'
    delete 'bin'
    delete 'Libs'
}

tasks.register("publishDemoToDocs") {
    group = 'Custom'
    description = "copy the source of the Demo testcases and its outputs into the rootProject/docs folder"
    doFirst {
        delete "$rootProjectDir/docs/assets/testOutput"
        delete "$rootProjectDir/docs/assets/Scripts"
    }
    doLast {
        copy {
            from("build/tmp/testOutput") {
                include "demo/**"
            }
            into "$rootProjectDir/docs/assets/testOutput"
        }
        copy {
            from("Scripts")
            into "$rootProjectDir/docs/assets/Scripts"
        }
    }
}
